
role(left).
role(right).

succ(a, b). succ(b, c).
succ(1, 2). succ(2, 3). succ(3, 4). succ(4, 5).

col(a). col(b). col(c).
row(1). row(2). row(3). row(4). row(5).

startcol(b).

firstcol(C) :-
    col(C),
    not succ(_, C).
lastcol(C) :-
    col(C),
    not succ(C, _).
firstrow(R) :-
    row(R),
    not succ(_, R).
lastrow(R) :-
    row(R),
    not succ(R, _).

cell((C, R)) :-
    col(C), row(R).

finishcell((C, R)) :-
    col(C), lastrow(R).

direction(north). direction(east). direction(south). direction(west).

cell_direction_cell((C, R2), north, (C, R1)) :-
    cell((C, R2)), cell((C, R1)),
    col(C), row(R2), row(R1),
    succ(R1, R2).

cell_direction_cell((C1, R), east, (C2, R)) :-
    cell((C1, R)), cell((C2, R)),
    col(C1), row(R), col(C2),
    succ(C1, C2).

cell_direction_cell((C, R1), south, (C, R2)) :-
    cell((C, R1)), cell((C, R2)),
    col(C), row(R1), row(R2),
    succ(R1, R2).

cell_direction_cell((C2, R), west, (C1, R)) :-
    cell((C2, R)), cell((C1, R)),
    col(C2), row(R), col(C1),
    succ(C1, C2).

crossing((Cell1, Cell2)) :-
    cell(Cell1), cell(Cell2),
    cell_direction_cell(Cell1, south, Cell2).

crossing((Cell1, Cell2)) :-
    cell(Cell1), cell(Cell2),
    cell_direction_cell(Cell1, east, Cell2).

quadrant((Cell, Right, Bottom, Diag)) :-
    cell(Cell), cell(Right), cell(Bottom), cell(Diag),
    cell_direction_cell(Cell, east, Right), cell_direction_cell(Cell, south, Bottom), cell_direction_cell(Right, south, Diag), cell_direction_cell(Bottom, east, Diag).

blocked(Role, Cell1, Cell2) :-
    role(Role), crossing((Cell1, Cell2)),
    true(border(Role, (Cell1, Cell2))).
blocked(Role, Cell1, Cell2) :-
    role(Role), cell(Cell1), cell(Cell2),
    blocked(Role, Cell2, Cell1).

unblockable(Role, ((C1, R),(C2, R))) :-
    role(Role), lastrow(R), col(C1), col(C2),
    crossing(((C1, R),(C2, R))).
unblockable(Role, Crossing) :-
    role(Role), crossing(Crossing),
    true(border(Role, Crossing)).
unblockable(Role, Crossing) :-
    role(Role), crossing(Crossing),
    critical(Role, Crossing).

critical(Role, Crossing) :-
    role(Role), crossing(Crossing),
    finishcell(Cell),
    not reachable(Role, Crossing, Cell).

reachable(Role, Crossing, Cell) :-
    true(at(Role, Cell)),
    role(Role), crossing(Crossing), cell(Cell).
reachable(Role, Crossing, Cell) :-
    role(Role), crossing(Crossing), cell(Cell),
    crossing((Cell, AdjacentCell)), distinct((Cell, AdjacentCell), Crossing),
    not blocked(Role, Cell, AdjacentCell),
    reachable(Role, Crossing, AdjacentCell).
reachable(Role, Crossing, Cell) :-
    role(Role), crossing(Crossing), cell(Cell),
    crossing((AdjacentCell, Cell)), distinct((AdjacentCell, Cell), Crossing),
    not blocked(Role, AdjacentCell, Cell),
    reachable(Role, Crossing, AdjacentCell).

init(at(Role, (StartCol, FirstRow))) :- role(Role), startcol(StartCol), firstrow(FirstRow).
init(control(left)).

next(control(Role2)) :-
    true(control(Role1)),
    role(Role2), role(Role1),
    distinct(Role1, Role2).

next(at(Role, Cell)) :-
    not does(Role, move(_Move)),
    true(at(Role, Cell)),
    role(Role), cell(Cell).

next(at(Role, Cell2)) :-
    does(Role, move(Direction)),
    true(at(Role, Cell1)),
    role(Role), cell(Cell1), cell(Cell2),
    cell_direction_cell(Cell1, Direction, Cell2).

next(border(Role2, Crossing)) :-
    does(Role1, block(Crossing)),
    role(Role2), crossing(Crossing), role(Role1),
    distinct(Role1, Role2).

next(border(Role, Crossing)) :-
    true(border(Role, Crossing)),
    role(Role), crossing(Crossing).

legal(Role, move(Direction)) :-
    true(at(Role, Cell1)),
    role(Role), direction(Direction),
    cell(Cell1), cell(Cell2),
    cell_direction_cell(Cell1, Direction, Cell2),
    not blocked(Role, Cell1, Cell2).

legal(Role2, block(Crossing)) :-
    not unblockable(Role1, Crossing),
    role(Role2), crossing(Crossing),
    role(Role1), distinct(Role1, Role2).

goal(Role1, 0) :-
    finished(Role2),
    not finished(Role1),
    role(Role1), role(Role2),
    distinct(Role1, Role2).

goal(Role1, 50) :-
    finished(Role1),
    finished(Role2),
    role(Role1), role(Role2),
    distinct(Role1, Role2).

goal(Role2, 100) :-
    finished(Role2),
    not finished(Role1),
    role(Role1), role(Role2),
    distinct(Role1, Role2).

finished(Role) :-
    true(at(Role, (_, LastRow))),
    role(Role), lastrow(LastRow).


terminal :- finished(_Role).
